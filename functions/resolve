#autoload

resolve() {
  local res cmd
  for cmd in "$@"; do
    # If it starts with backslash, remove that and don't do alias lookup
    if [[ "$cmd" = \\* ]]; then
      res=${cmd#\\}
    else
      res="$(which $cmd)"
    fi
    case "$res" in
      (*:*aliased*)
        echo "$res"
        res="${res##*: aliased to }"
        res="${res%% *}" # Strip off arguments

        # If it now matches the command, prevent alias expansion on next attempt
        [[ "$res" = "$cmd" ]] && res=\\"$res"
        resolve "$res"
        ;;
      (*)
        [[ "$res" = /* ]] || res=$commands[$res]
        echo "$res"
        if [[ -L "$res" ]]; then
          resolve $(perl -MCwd -e 'print Cwd::abs_path($ARGV[0])' "$res")
        else
          real=$(perl -MCwd -e 'print Cwd::abs_path($ARGV[0])' "$res")
          [[ $real = $res ]] || echo $real
        fi
    esac
  done
}

[ -n "$ZSH_NAME" ] && resolve "$@"
