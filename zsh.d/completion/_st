#compdef st

_ssh # Ensure that _ssh_hosts gets defined

# Explicitly define function so that above only gets called once
_st() {
  local context state line
  typeset -A opt_args
  local components

  _arguments \
    '1:Host:->host' \
    '2:Session:->session' \
    && return 0

  case "$state" in
    (host)
      _ssh_hosts
      ;;
    (session)
      local -a sessions
      local host=$line[1]
      local -a remote=()
      if [ -n "$host" ] && [ "-" != "$host" ]; then
        remote=(ssh -q -o BatchMode=yes "$host")
      fi

      sessions=( ${${(f)"$($remote tmux 2> /dev/null list-sessions)"}/:[ $'\t']##/:} )
      _describe -t sessions 'sessions' sessions "$@"
      ;;
  esac
}

_st "$@"
