#autoload

resolve() {
  local res cmd
  for cmd in "$@"; do
    # If it starts with backslash, remove that and don't do alias lookup
    if [[ "$cmd" = \\* ]]; then
      res=${cmd#\\}
    elif (( $+functions[$cmd] )); then
      which "$cmd"
      continue
    else
      res="$(which $cmd)"
    fi
    case "$res" in
      (*shell built-in*)
        echo $res
        ;;
      (*:*aliased*)
        echo "$res"
        res="${res##*: aliased to }"
        res="${res%% *}" # Strip off arguments

        # If it now matches the command, prevent alias expansion on next attempt
        [[ "$res" = "$cmd" ]] && res=\\"$res"
        resolve "$res"
        ;;
      (*)
        [[ "$res" = /* ]] || res=$commands[$res]
        command resolve "$res"
    esac
  done
}

[ -n "$ZSH_NAME" ] && resolve "$@"
